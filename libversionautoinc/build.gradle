apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'java-gradle-plugin'

group = 'xyz.ilyasergeev'
version = '0.22'
def libname = 'version-auto-inc'
def artifactProjectID = 'autoink'

gradlePlugin {
    plugins {
        greetingsPlugin {
            id = "${group}.${libname}"
            implementationClass = 'xyz.ilyasergeev.libversionautoinc.IncrementPlugin'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly "com.android.tools.build:gradle:3.3.0"
}

publish {

    def groupProjectID = group
    def publishVersionID = version

    userOrg = 'ilyasergeev'
    repoName = 'repo'
    groupId = groupProjectID
    artifactId = artifactProjectID
    publishVersion = publishVersionID
    desc = 'First Sample of Android Studio plugin'
    website = 'https://github.com/IlyaSergeev/VersionAutoIncrementor'
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// Отправляем
uploadArchives {

    // Перечисляем репозитории, куда отправляем
    repositories {

        mavenDeployer {

            // Готовим pom, добавляем в него наши метаданные
            pom.project {
                name libname
                artifactId artifactProjectID
                packaging 'jar'
                description 'Version incrementing plugin.'
                url 'https://github.com/IlyaSergeev/VersionAutoIncrementor'

                // Информация о расположении исходников
                scm {
                    url 'https://github.com/IlyaSergeev/VersionAutoIncrementor'
                }

                // Информация о лицензии
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                // Информация о разработчиках
                developers {
                    developer {
                        id 'ilyasergeev'
                        name 'Ilya Sergeev'
                        email 'ilya.sergeev@icloud.com'
                    }
                }
            }

            // Путь до нашего локального репозитория
            // app - папка нашего модуля андроид приложения
            repository(url: uri('../app/repo'))

        }

    }

}